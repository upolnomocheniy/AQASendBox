plugins {
    id 'java'
    id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.25'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    implementation(
            'io.cucumber:cucumber-picocontainer:7.11.1',
            'com.codeborne:selenide:6.11.2',
            'org.assertj:assertj-core:3.24.2'
    )

    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.8.1',
            'io.cucumber:cucumber-java:7.11.1',
            'io.cucumber:cucumber-junit:7.11.1',
            'com.codeborne:selenide:6.11.2',
            'org.assertj:assertj-core:3.24.2'
    )
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            System.setProperty('selenide.browser', 'chrome')
            System.setProperty('selenide.startMaximized', 'true')
            systemProperties = System.getProperties();
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'json:build/cucumber-report.json',
                    '--plugin', 'timeline:build/time-line',
                    '--tags', System.getProperty('tags', '@ppe'),
                    '--glue', 'testFeature.definitionSteps',
                    'src/test/resources'
            ]
            ignoreExitValue = true
        }
    }
}

test {
    useJUnitPlatform()
}

cucumber.finalizedBy(generateCucumberReports)

cucumberReports {
    outputDir = file('build')
    buildId = '0'
    reports = files('build/cucumber-report.json')
    testTasksFinalizedByReport = false
}